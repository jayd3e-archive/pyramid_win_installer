; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{59C9672A-71E4-405E-92C2-6F9BBA98F453}
AppName=Pyramid
AppVersion=1.3

AppVerName=Pyramid 1.3
AppPublisher=Pylons Project
AppPublisherURL=http://www.pylonsproject.org/
AppSupportURL=http://www.pylonsproject.org/
AppUpdatesURL=http://www.pylonsproject.org/
CreateAppDir=no
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
ChangesEnvironment=true

[Code]
#include "it_download.iss"

const
  ModPathName = 'modifypath'; 
  ModPathType = 'system';
var
  CreateStarterPyramidApp: Boolean; // Whether or not we need to create a starter app
  StarterAppPage: TInputQueryWizardPage;  // Page for accepting the start app name

{* A utility function to return the starter app's name *}
function GetAppName(Default: String): string;
begin
  Result := StarterAppPage.Values[0];
end;

{* Check to see if we need to create a starter app *}
function CreateStarterAppCheck(): Boolean;
begin
  Result := CreateStarterPyramidApp;
end;

{* Toggle the starter app name input box *}
procedure ClickEvent(Sender : TObject);
begin
  if StarterAppPage.Edits[0].Enabled = False then
  begin
    CreateStarterPyramidApp := True; 
    StarterAppPage.Edits[0].Enabled := True;
  end
  else
  begin
    CreateStarterPyramidApp := False; 
    StarterAppPage.Edits[0].Enabled := false;
  end;
end;

{* Create the starter app name input page *}
procedure CreateStarterAppPage;
var
  Checkbox: TCheckBox;
begin
  CreateStarterPyramidApp := True;
  StarterAppPage := CreateInputQueryPage(wpSelectTasks, 'Create a Pyramid Starter App', 'Enter the name for your Pyramid starter app.', '');
  
  CheckBox := TNewCheckBox.Create(StarterAppPage);
  CheckBox.Top := 5;
  Checkbox.Width := 200;
  CheckBox.Caption := 'Created starter app?';
  CheckBox.Checked := True;
  CheckBox.Parent := StarterAppPage.Surface;
  CheckBox.OnClick := @ClickEvent;
  
  StarterAppPage.Add('App Name:', False);
  StarterAppPage.Values[0] := 'MyApp';
end;


{* Add to Python to our path *}
function ModPathDir(): TArrayOfString; 
begin
  setArrayLength(Result, 1) 
  Result[0] := ExpandConstant('C:\Python26');
end;
#include "modpath.iss"

{* Initialize the wizard *}
procedure InitializeWizard();
begin
  ITD_Init();
  ITD_AddFile('http://www.python.org/ftp/python/2.6/python-2.6.msi', ExpandConstant('{tmp}\python-2.6.msi'));
  ITD_AddFile('http://peak.telecommunity.com/dist/ez_setup.py', ExpandConstant('{tmp}\ez_setup.py'));
  ITD_DownloadAfter(wpReady);
  
  CreateStarterAppPage();
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Run]
Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\python-2.6.msi"" /qn"; StatusMsg: "Installing Python-2.6..."; Flags: "runhidden"
Filename: "C:\Python26\python.exe"; Parameters: """{tmp}\ez_setup.py"""; StatusMsg: "Installing setuptools..."; Flags: "runhidden"
Filename: "C:\Python26\Scripts\easy_install.exe"; Parameters: "virtualenv"; StatusMsg: "Installing virtualenv..."; Flags: "runhidden"
Filename: "C:\Python26\Scripts\virtualenv.exe"; Parameters: "C:\Projects\{code:GetAppName}\env"; Check: CreateStarterAppCheck; StatusMsg: "Creating your virtualenv..."; Flags: "runhidden"
Filename: "C:\Projects\{code:GetAppName}\env\Scripts\easy_install.exe"; Parameters: "pyramid"; Check: CreateStarterAppCheck; StatusMsg: "Installing Pyramid...."; Flags: "runhidden"
Filename: "C:\Projects\{code:GetAppName}\env\Scripts\pcreate.exe"; Parameters: "C:\Projects\{code:GetAppName} -t starter"; Check: CreateStarterAppCheck; StatusMsg: "Creating a starter application..."; Flags: "runhidden"
Filename: "C:\Projects\{code:GetAppName}\env\Scripts\python.exe"; Parameters: "C:\Projects\{code:GetAppName}\setup.py develop"; Check: CreateStarterAppCheck; StatusMsg: "Installing dependencies of starter application..."; Flags: "runhidden"

[Tasks]
Name: modifypath; Description: Add Python26 to your environmental path;